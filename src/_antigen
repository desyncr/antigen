#compdef _antigen
# Setup antigen's autocompletion
_antigen () {
  local -a _1st_arguments
  _1st_arguments=(
    'apply:Load all bundle completions'
    'bundle:Install and load the given bundle'
    'bundles:Bulk define bundles with HEREDOC syntax'
    "cache-gen:Generate Antigen's cache with currently loaded bundles"
    'cleanup:Purge clones of bundles currently not loaded'
    'env:Display Antigen environment variables'
    'init:Use caching to quickly load bundles'
    'list:List currently loaded bundles'
    'purge:Remove a bundle from the filesystem'
    'reset:Clean generated cache and completions'
    'restore:Restore bundle state from a snapshot file'
    "revert:Revert bundles to their state prior to the last time 'antigen update' was run"
    'selfupdate:Update Antigen itself'
    'snapshot:Create a snapshot of all active bundle repos'
    'theme:Switch the prompt theme'
    'update:Update bundles'
    'use:Load a supported zsh pre-packaged framework'
  );

  _1st_arguments+=(
    'help:Show this message'
    'version:Display Antigen version'
  )

  __bundle() {
    _arguments \
      '--loc[Path to the location <path-to/location>]' \
      '--url[Path to the repository <github-account/repository>]' \
      '--branch[Git branch name]' \
      '--no-local-clone[Do not create a clone]'
  }
  __list() {
    _arguments \
      '--simple[Show only bundle name]' \
      '--short[Show only bundle name and branch]' \
      '--long[Show bundle records]'
  }


  __cleanup() {
    _arguments \
      '--force[Do not ask for confirmation]'
  }

  _arguments '*:: :->command'

  if (( CURRENT == 1 )); then
    _describe -t commands "antigen command" _1st_arguments
    return
  fi

  local -a _command_args
  case "$words[1]" in
    bundle)
      __bundle
      ;;
    use)
      compadd "$@" "oh-my-zsh" "prezto"
      ;;
    cleanup)
      __cleanup
      ;;
    (update|purge)
      compadd $(type -f \-antigen-get-bundles &> /dev/null || antigen &> /dev/null; -antigen-get-bundles --simple 2> /dev/null)
      ;;
    theme)
      compadd $(type -f \-antigen-get-themes &> /dev/null || antigen &> /dev/null; -antigen-get-themes 2> /dev/null)
      ;;
    list)
      __list
    ;;
  esac
}
